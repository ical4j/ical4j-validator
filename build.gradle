plugins {
    id 'java'
    id "biz.aQute.bnd.builder" version "$bndVersion"
    id 'maven-publish'
    id 'signing'
    id 'pl.allegro.tech.build.axion-release' version '1.13.6'
}

repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    implementation "org.mnode.ical4j:ical4j:$ical4jVersion"

    implementation "org.slf4j:slf4j-api:$slf4jVersion",
            "org.slf4j:slf4j-log4j12:$slf4jVersion",
            "org.apache.logging.log4j:log4j:$log4jVersion"

    implementation ("org.eclipse.jetty:jetty-server:$jettyVersion") {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    implementation ("org.eclipse.jetty:jetty-servlet:$jettyVersion") {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    implementation "info.picocli:picocli:$picocliVersion"

//    compileOnly "biz.aQute.bnd:biz.aQute.bndlib:$bndVersion"
    compileOnly 'org.osgi:osgi.core:8.0.0',
            'org.osgi:org.osgi.service.component.annotations:1.5.0',
            'org.osgi:org.osgi.service.metatype.annotations:1.4.1',
            'org.osgi:org.osgi.annotation:6.0.0'

}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

scmVersion {
    tag {
        prefix = 'ical4j-validator-'
    }
    versionCreator 'versionWithBranch'
    branchVersionCreator = ['main': 'simple']
}

group = 'org.ical4j'
description = 'Validation reporting of iCalendar data'
version = scmVersion.version

ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

publishing {
    publications {
        ical4j_validator(MavenPublication) {
            from components.java
            artifact javadocJar
            artifact sourcesJar
            pom.withXml {
                asNode().appendNode('name', project.name)
                asNode().appendNode('description', project.description)
                asNode().appendNode('url', 'http://ical4j.github.io')

                def scmNode = asNode().appendNode('scm')
                scmNode.appendNode('url', 'https://github.com/ical4j/ical4j-validator')
                scmNode.appendNode('connection', 'scm:git@github.com:ical4j/ical4j-validator.git')
                scmNode.appendNode('developerConnection', 'scm:git@github.com:ical4j/ical4j-validator.git')

                def licenseNode = asNode().appendNode('licenses').appendNode('license')
                licenseNode.appendNode('name', 'iCal4j - License')
                licenseNode.appendNode('url', 'https://raw.githubusercontent.com/ical4j/ical4j/master/LICENSE')
                licenseNode.appendNode('distribution', 'repo')

                def developerNode = asNode().appendNode('developers').appendNode('developer')
                developerNode.appendNode('id', 'fortuna')
                developerNode.appendNode('name', 'Ben Fortuna')
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = version.endsWith('SNAPSHOT') ? "https://s01.oss.sonatype.org/content/repositories/snapshots/" : "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

signing {
    required { isReleaseVersion }
    sign publishing.publications.ical4j_validator
}
